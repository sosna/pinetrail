ext.projectIds = ['group': 'ws.sosna.pinetrail', 'version': '1.0.0-SNAPSHOT']

allprojects {
    group = projectIds.group
    version = projectIds.version
    
    repositories {
        mavenCentral()
    }
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.11'
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

subprojects {
    ext.configDir = new File(rootDir, 'config')
    
    apply plugin: 'java'    
    apply from: "$rootDir/gradle/jacoco.gradle"
    apply from: "$rootDir/gradle/checkstyle.gradle"
    apply from: "$rootDir/gradle/pmd.gradle"
    apply from: "$rootDir/gradle/findbugs.gradle"
    apply from: "$rootDir/gradle/jdepend.gradle"
    apply plugin: 'info.solidsoft.pitest'
    
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    
    dependencies {
        compile 'org.slf4j:slf4j-api:1.7.25'
        testCompile 'junit:junit:4.12'
        runtime 'ch.qos.logback:logback-classic:1.2.3'
    }
    
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
    
    pitest {
        targetClasses = ['ws.sosna.pinetrail.*']
        threads = 4
        outputFormats = ['XML', 'HTML']
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.9'
}
